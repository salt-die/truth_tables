?exp: factor
    | exp ("or" | "|") factor                -> or_
    | exp ("implies" | "->" | "then") factor -> implies
    | exp ("iff" | "<->") factor             -> iff
    | exp ("xor" | "^") factor               -> xor
?factor: term
    | factor ("and" | "&") term              -> and_
?term: ("F" | "0")                           -> false
    | ("T" | "1")                            -> true
    | /[a-z]+/                               -> var
    | "(" exp ")"
    | ("not" | "~") term                     -> neg

%import common.WS
%ignore WS